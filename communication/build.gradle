plugins {
    id 'java-library'
}

group 'com.bnpparibas.gban.communication'
version '0.1-SNAPSHOT'


ext {
    flatbuffersVersion = '2.0.3'
    grpcVersion = '1.48.1'

    fbsFlattenDir = "${buildDir}/generated/sources/fbs"
}

task flattenIncludes() {
    doLast {
        copy {
            from("${projectDir}/src/main/flatbuffers") {
                include "**/*.fbs"
            }
            into(fbsFlattenDir)
            //Exclude lowercase shared files
            exclude(resource -> resource.file.isFile() && resource.file.name.matches(/^[a-z0-9]{1}.*$/))
            filter {
                    //Flattering include statements
                String line ->
                    if (line.startsWith("include")) {
                        project.logger.info("Flattering include `${line}`")
                        def file = "${projectDir}/src/main/flatbuffers/${line.split(' ')[1]}"
                        project.logger.info("With content from file `${file}`")
                        return new File(file).getText('UTF-8')
                    }
                    return line
            }
        }
    }
}

task flatc() {
    dependsOn(flattenIncludes)
    doLast {
        def flattenedSources = new File(fbsFlattenDir)
        flattenedSources.eachFileRecurse() {
            file -> if(file.isFile()){
                //compile endpoint fbs files
                exec {
                    workingDir = fbsFlattenDir
                    commandLine = "${projectDir}/flatc"
                    args = ["--grpc", "-j", "-o", "${buildDir}/generated/sources/flatbuffers/java/main/", file.path]
                }
            }
        }
    }
}

sourceSets {
    main {
        java {
            srcDir("${buildDir}/generated/sources/flatbuffers/java/main/")
        }
    }
}

compileJava {
    dependsOn(flatc)
}

dependencies {
    api 'javax.annotation:javax.annotation-api:1.3.2' //used by generated classes
    api "com.google.flatbuffers:flatbuffers-java:$flatbuffersVersion" //used by generated classes
    api "com.google.flatbuffers:flatbuffers-java-grpc:$flatbuffersVersion" //used by generated classes
    api "io.grpc:grpc-services:$grpcVersion" //used by generated classes

    api "io.grpc:grpc-netty:$grpcVersion" //used by client and server
}